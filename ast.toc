
(add-ns c (git-dependency "https://github.com/Toccata-Lang/constraints.git"
                          "constraints.toc"
                          :sha "ff0ac6e"))

(def SymbolOrString (any-of Symbol
                            String))

(deftype Location [file line]
  (assert (instance? SymbolOrString file))
  (assert (instance? Integer line))

  Stringable
  (string-list [_]
    (list (either (and (= file 'core)
                       (maybe "$TOCCATA_DIR/core.toc"))
                  (str file))
          ", " (str line))))

(defprotocol FileLineInfo
  (file-name [ast]
    (assert-result x (instance? SymbolOrString x))
    "")

  (line-number [ast]
    0))

;; TODO: combine with FileLineInfo
(defprotocol FileLoc
  (location [ast]
    (Location (file-name ast) (line-number ast))))

(extend-type Location
  FileLineInfo
  (file-name [ast]
    (assert-result x (instance? SymbolOrString x))
    (.file ast))

  (line-number [ast]
    (.line ast)))

;; we tag symbols with file/line for use later
(defprotocol Tagged
  (namespace [s]
    (assert-result r (instance? (maybe-of Symbol) r)))

  (tag [s]
    (tag s "" 0))

  (tag [s file line])

  (untag [x]
    (assert-result r (instance? Symbol r))))

(deftype tagged-symbol [ns base sym file line]
  (assert (instance? SymbolOrString file))
  (assert (instance? (maybe-of Symbol) ns))
  (assert (instance? Symbol sym))
  (assert (instance? Symbol base))
  (assert (instance? Integer line))

  FileLineInfo
  (file-name [s] file)
  (line-number [s] line)

  Hashable
  (sha1 [x] (sha1 base))

  Ord
  (<* [_ x] (<* x base))

  Eq
  (=* [_ x] (=* x base))

  Stringable
  (string-list [s]
    (list (either (map (.ns s) (fn [ns-str] (str ns-str "/" (untag (.base s)))))
                  (str (untag (.base s))))))
  
  Tagged
  (namespace [s] (.ns s))
  (tag [s] s)
  (tag [s new-file new-line]
    (tagged-symbol (.ns s) (.base s) (.sym s) new-file new-line))
  (untag [s] (.base s)))

(extend-type String
  Tagged
  (tag [s]
    (let [s (symbol s)]
      (tagged-symbol nothing s s "" 0)))
  (tag [s file line]
    (let [s (symbol s)]
      (tagged-symbol nothing s s file line))))

(extend-type Symbol
  Tagged
  (namespace [s]
    nothing)
  (tag [s]
    (tagged-symbol nothing s s "" 0))
  (tag [s file line]
    (tagged-symbol nothing s s file line))
  (untag [s] s))

(extend-type Sequence
  FileLineInfo
  (file-name [v]
    (either (some v (fn [x]
                      (let [file (file-name x)]
                        (and (or (= file 'core)
                                 (first file))
                             (maybe file)))))
            ""))

  (line-number [v]
    (either (some v (fn [x]
                      (let [file (file-name x)]
                        (and (or (= file 'core)
                                 (first file))
                             (maybe (line-number x))))))
            0)))

(extend-type HashMap
  FileLineInfo
  (file-name [m]
    (file-name (seq m)))
  (line-number [m]
    (line-number (seq m))))

(deftype block-comment-ast [lines path-to-file line-num]
  (assert (instance? (vector-of String) lines))
  (assert (instance? SymbolOrString path-to-file))
  (assert (instance? Integer line-num))

  Stringable
  (string-list [_] (comp (list "<BlockCommentAST")
                         (interpose lines "\n")
                         (list ">")))

  FileLineInfo
  (file-name [_] path-to-file)
  (line-number [_] line-num))

(defn block-comment [path-to-file line-num lines]
  (block-comment-ast lines path-to-file line-num))


(deftype integer-ast [int path-to-file line-num]
  (assert (instance? Integer int))
  (assert (instance? SymbolOrString path-to-file))
  (assert (instance? Integer line-num))

  Stringable
  (string-list [_] (list (str int)))

  FileLineInfo
  (file-name [_] path-to-file)
  (line-number [_] line-num))


(deftype string-ast [string path-to-file line-num]
  (assert (instance? String string))
  (assert (instance? SymbolOrString path-to-file))
  (assert (instance? Integer line-num))

  Stringable
  (string-list [_] (list "\"" string "\""))

  FileLineInfo
  (file-name [_] path-to-file)
  (line-number [_] line-num))

(deftype inline-ast [lang result-type txt path-to-file line-num]
  (assert (instance? tagged-symbol lang))
  (assert (instance? c/ValueConstraint result-type))
  (assert (instance? String txt))
  (assert (instance? SymbolOrString path-to-file))
  (assert (instance? Integer line-num))

  Stringable
  (string-list [_]
    (list "(inline " (str lang) " " (str result-type) " \"" txt "\")"))

  FileLineInfo
  (file-name [_] path-to-file)
  (line-number [_] line-num))

(defn inline [lang txt result-type path-to-file line-num]
  (inline-ast lang txt result-type path-to-file line-num))


(deftype params-ast [fixed variadic]
  (assert (instance? (vector-of (any-of tagged-symbol
                                        params-ast))
                     fixed))
  (assert (instance? (maybe-of tagged-symbol)
                     variadic))

  Stringable
  (string-list [_]
    (comp (list "[")
          (flat-map (interpose fixed " ") string-list)
          (either (map variadic (fn [variadic]
                                  (cons " & " (string-list variadic))))
                  empty-list)
          (list "]")))

  Tagged
  (tag [_ file line]
    (params-ast (map fixed (fn [x]
                             (tag x file line)))
                (map variadic (fn [x]
                                (tag x file line)))))

  Collection
  (count [_] (count fixed))

  FileLineInfo
  (file-name [_] (file-name fixed))
  (line-number [_] (line-number fixed)))

(def ParamType (any-of tagged-symbol
                       params-ast))

(defn params
  ([fixed] (params-ast fixed nothing))
  ([fixed variadic] (params-ast fixed variadic)))


(deftype main-ast [params body]
  (assert (instance? params-ast params))
  (assert (instance? Vector body))

  Stringable
  (string-list [_] (comp (list "<MainAST ")
                         (string-list params)
                         (list " ")
                         (string-list body)
                         (list ">")))

  FileLineInfo
  (file-name [_] (file-name body))
  (line-number [_] (line-number body)))

(defn main-fn [params body]
  (main-ast params body))


(deftype declaration-ast [sym]
  (assert (instance? tagged-symbol sym))

  Stringable
  (string-list [_] (list "<DeclareAST " (str sym) ">"))

  FileLineInfo
  (file-name [_] (file-name sym))
  (line-number [_] (line-number sym)))

(defn declare [sym]
  (declaration-ast sym))


(deftype fn-arity-ast [fn-sym fn-var params doc body param-consts result-const]
  (assert (instance? params-ast params))
  (assert (instance? Vector body))
  (assert (instance? c/ItemsConstraint param-consts))
  (assert (instance? c/ValueConstraint result-const))

  Stringable
  (string-list [_] (comp (string-list params)
                         (list "\n")
                         (flat-map (interpose body "\n") string-list)))

  FileLineInfo
  (file-name [_] (file-name body))
  (line-number [_] (line-number body)))

(defn fn-arity [params doc body]
  (fn-arity-ast "" "" params doc body c/empty-items-constraint c/top-type))


(deftype fn-ast [fn-sym arities]
  (assert (instance? Maybe fn-sym))
  (assert (instance? Vector arities))

  Type
  (type-name [x]
    (str "fn-ast: " fn-sym))

  Stringable
  (string-list [_]
    (comp (list "(fn " (str (either fn-sym "")) " ")
          (cond (= 1 (count arities))
                (flat-map (seq arities) string-list)
                (flat-map (seq arities) (fn [arity]
                                          (comp (list "(")
                                                (string-list arity)
                                                (list ")")))))
          (list  ")")))

 FileLineInfo
  (file-name [_] (file-name arities))
  (line-number [_] (line-number arities)))

(defn fn-expr [sym arities]
  (fn-ast sym arities))


(deftype call-ast [call-target args]
  (assert (instance? Vector args))

  Stringable
  (string-list [_]
    (comp (list "(" (str call-target) " ")
          (flat-map (interpose (seq args) " ") string-list)
          (list ")")))

  FileLineInfo
  (file-name [_] (file-name args))
  (line-number [_] (line-number args)))

(defn call-expr [[target & args]]
  (call-ast target (vec args)))

(def BindingTarget (any-of tagged-symbol
                           params-ast))

(deftype binding-ast [binding val]
  (assert (instance? BindingTarget binding))

  Stringable
  (string-list [_]
    (list (str binding) " " (str val)))

  FileLineInfo
  (file-name [_] (file-name val))
  (line-number [_] (line-number val)))

(defn binding [binding val]
  (binding-ast binding val))

(deftype let-ast [bindings body]
  (assert (instance? (vector-of binding-ast) bindings))
  (assert (instance? Vector body))

  Stringable
  (string-list [_]
    (list "(let [" (to-str (interpose bindings "\n")) "]\n" (apply str (interpose body "\n")) ")"))

  FileLineInfo
  (file-name [_] (file-name body))
  (line-number [_] (line-number body)))

(defn let-expr [bindings body]
  (let-ast bindings body))


(deftype and-ast [clauses]
  (assert (instance? (all-of Vector
                             (min-count 1))
                     clauses))

  Stringable
  (string-list [_]
    (comp (list "(and ")
          (flat-map (interpose clauses "\n") string-list)
          (list ")")))

  FileLineInfo
  (file-name [_] (extract (map (first clauses) file-name)))
  (line-number [_] (extract (map (first clauses) line-number))))

(defn and-expr [clauses]
  (and-ast clauses))


(deftype or-ast [clauses]
  (assert (instance? Vector clauses))

  Stringable
  (string-list [_]
    (comp (list "(or ")
          (flat-map (seq clauses) string-list)
          (list ")")))

  FileLineInfo
  (file-name [_] (extract (map (first clauses) file-name)))
  (line-number [_] (extract (map (first clauses) line-number))))

(defn or-expr [clauses]
  (or-ast clauses))


(deftype either-ast [clause alt]
  Stringable
  (string-list [_]
    (comp (list "(either ")
          (string-list clause)
          (list "\n")
          (string-list alt)
          (list ")")))

  FileLineInfo
  (file-name [_] (file-name clause))
  (line-number [_] (line-number clause)))

(defn either-expr [clause alt]
  (either-ast clause alt))

(defn file-name-from-path [path]
  (map (last path)
       (fn [[file]] file)))

(defn line-number-from-path [path]
  (map (last path)
       (fn [[_ ln]] ln)))

(extend-type c/TypeConstraint
  FileLineInfo
  (file-name [c]
    (let [file (file-name (.type-sym c))]
      (either (or (= file 'core)
                  (and (empty? file)
                       (file-name-from-path (.path c))))
              file)))

  (line-number [c]
    (let [ln (line-number (.type-sym c))]
      (either (and (= 0 ln)
                   (line-number-from-path (.path c)))
              ln))))

(extend-type c/NoValues
  FileLineInfo
  (file-name [c] (file-name (.constraints c)))
  (line-number [c] (line-number (.constraints c))))

(extend-type c/MultiConstraint
  FileLineInfo
  (file-name [c] (file-name (.constraints c)))
  (line-number [c] (line-number (.constraints c))))

(extend-type c/InferredInner
  FileLineInfo
  (file-name [c]
    (either (file-name-from-path (.path c))
            (file-name (.contents c))))
  (line-number [c] 0))

(extend-type c/CollectionOf
  FileLineInfo
  (file-name [c]
    (either (file-name-from-path (.path c))
            (file-name (.contents c))))
  (line-number [c] 0))

(extend-type c/ItemsConstraint
  FileLineInfo
  (file-name [c]
    (either (file-name-from-path (.path c))
            (file-name (.items-constraints c))))
  (line-number [c] 0))

(extend-type c/FieldConstraint
  FileLineInfo
  (file-name [c]
    (either (file-name-from-path (.path c))
            ""))
  (line-number [c]
    (either (line-number-from-path (.path c))
            0)))

(extend-type c/StaticIntConstraint
  FileLineInfo
  (file-name [c]
    (either (file-name-from-path (.path c))
            ""))
  (line-number [c]
    (either (line-number-from-path (.path c))
            0)))

(extend-type c/MinValue
  FileLineInfo
  (file-name [c]
    (either (file-name-from-path (.path c))
            ""))
  (line-number [c]
    (either (line-number-from-path (.path c))
            0)))

(extend-type c/MaxValue
  FileLineInfo
  (file-name [c]
    (either (file-name-from-path (.path c))
            ""))
  (line-number [c]
    (either (line-number-from-path (.path c))
            0)))

(extend-type c/StaticLengthConstraint
  FileLineInfo
  (file-name [c]
    (either (file-name-from-path (.path c))
            ""))
  (line-number [c]
    (either (line-number-from-path (.path c))
            0)))

(extend-type c/StaticStrConstraint
  FileLineInfo
  (file-name [c]
    (either (file-name-from-path (.path c))
            ""))
  (line-number [c]
    (either (line-number-from-path (.path c))
            0)))

(extend-type c/SumConstraint
  FileLineInfo
  (file-name [c]
    (either (file-name-from-path (.path c))
            (file-name (.alts c))))
  (line-number [c]
    (either (line-number-from-path (.path c))
            (line-number (.alts c)))))

(extend-type c/KeysConstraint
  FileLineInfo
  (file-name [c]
    (either (file-name-from-path (.path c))
            (file-name (.constraint c))))
  (line-number [c]
    (either (line-number-from-path (.path c))
            (line-number (.constraint c)))))

(extend-type c/ResultConstraint
  FileLineInfo
  (file-name [c]
    (file-name (.assertion c)))
  (line-number [c]
    (line-number (.assertion c))))

(defn result-expr [assertion]
  (c/ResultConstraint assertion))


(deftype prototype-ast [fn-name params doc default-body]
  (assert (instance? params-ast params))
  (assert (instance? block-comment-ast doc))
  (assert (instance? Vector default-body))

  Stringable
  (string-list [_] (list "<PrototypeFnAST " (str fn-name) ">"))

  FileLineInfo
  (file-name [_] (file-name fn-name))
  (line-number [_] (line-number fn-name)))

(defn prototype [fn-name args doc default-body]
  (prototype-ast fn-name args doc default-body))


(deftype protocol-ast [protocol-sym prototypes]
  (assert (instance? tagged-symbol protocol-sym))
  (assert (instance? Vector prototypes))

  Stringable
  (string-list [_] (list "<ProtocolAST " (str protocol-sym) ">"))

  FileLineInfo
  (file-name [_] (file-name protocol-sym))
  (line-number [_] (line-number protocol-sym)))

(defn protocol [sym prototypes]
  (protocol-ast sym prototypes))


(deftype extend-ast [type impls]
  (assert (instance? tagged-symbol type))
  (assert (instance? HashMap impls))

  Stringable
  (string-list [_] (list "<ExtendAST " (str type) ">"))

  FileLineInfo
  (file-name [_] (file-name type))
  (line-number [_] (line-number type)))

(defn type-extension [type impls]
 (extend-ast type (either (map (empty? impls) (fn [_] {}))
                          (apply comp (seq (filter impls (partial instance? HashMap)))))))


(deftype quoted-ast [q-val]
  Stringable
  (string-list [_]
    (list (str "'" q-val)))

  Ord
  (<* [x y]
    (assert (instance? quoted-ast y))
    (and (< q-val (.q-val x))
         (maybe x)))

  FileLineInfo
  (file-name [_] (file-name q-val))
  (line-number [_] (line-number q-val)))

(defn quoted [q-val]
  (quoted-ast q-val))


(deftype reify-ast [type-num impls]
  (assert (instance? (maybe-of Integer) type-num))
  (assert (instance? HashMap impls))

  Stringable
  (string-list [_] (list "(reify " (str type-num " \n" impls) ")"))

  FileLineInfo
  (file-name [_] (file-name impls))
  (line-number [_] (line-number impls)))

(defn reified [type-num impls]
  (reify-ast type-num impls))

(deftype cond-val-ast [conditional value]
  Stringable
  (string-list [_]
    (list (str conditional " " value)))

  FileLineInfo
  (file-name [ast]
    (file-name conditional))

  (line-number [ast]
    (line-number conditional)))

(defn cond-val-expr [conditional value]
  (cond-val-ast conditional value))

(deftype cond-ast [conditionals default]
  (assert (instance? (all-of (vector-of cond-val-ast)
                             (min-count 1))
                     conditionals))

  Stringable
  (string-list [_]
    (list "(cond " (to-str (flat-map conditionals (fn [c-v]
                                                    [(str c-v) "\n"])))
          (str default) ")\n"))

  FileLineInfo
  (file-name [ast]
    (file-name conditionals))

  (line-number [ast]
    (line-number conditionals)))

(defn cond-expr [conditionals default]
  (cond-ast conditionals default))

(deftype type-ast [sym fields impls]
  (assert (instance? tagged-symbol sym))
  (assert (instance? params-ast fields))
  (assert (instance? Vector impls))

  Stringable
  (string-list [_] (list "<TypeAST " (str sym) ">"))

  FileLineInfo
  (file-name [_] (file-name sym))
  (line-number [_] (line-number sym)))

(defn type-expr [sym fields impls]
  (type-ast sym fields impls))

(deftype module-ast [file-path]
  (assert (instance? String file-path)))

(defn module [file-path]
  (module-ast file-path))


(deftype git-dep-ast [repo file args]
  (assert (instance? String repo))
  (assert (instance? String file))
  (assert (instance? HashMap args))

  FileLineInfo
  (file-name [_] (file-name repo))
  (line-number [_] (line-number repo)))

(defn git-dep [repo file args]
  (git-dep-ast repo file args))


(deftype add-ns-ast [ns-sym mod]
  (assert (instance? tagged-symbol ns-sym))

  Stringable
  (string-list [_] (list "<AddNS " (str ns-sym) ">"))

  FileLineInfo
  (file-name [_] (file-name ns-sym))
  (line-number [_] (line-number ns-sym)))

(defn ns-add [ns mod]
  (add-ns-ast ns mod))

(deftype definition-ast [sym value-exprs]
  (assert (instance? tagged-symbol sym))

  Stringable
  (string-list [_]
    (comp (list "(def " (str sym) "\n")
          (interpose (map value-exprs str) "\n")
          (list ")")))

  FileLineInfo
  (file-name [_] (file-name sym))
  (line-number [_] (line-number sym)))

(defn definition [sym value]
  (definition-ast sym value))

(deftype JS-callable [sym num-args]
  Stringable
  (string-list [_]
    (list "(JS-callable " (str sym) ", " (str num-args) ")"))

  FileLineInfo
  (file-name [_] (file-name sym))
  (line-number [_] (line-number sym)))

(def CodeAST (any-of inline-ast
                     let-ast
                     and-ast
                     or-ast
                     either-ast
                     quoted-ast
                     reify-ast
                     call-ast
                     fn-ast
                     string-ast
                     integer-ast
                     Symbol
                     String
                     Integer))

(def NoCode (any-of c/ResultConstraint
                    block-comment-ast
                    c/ValueConstraint))
